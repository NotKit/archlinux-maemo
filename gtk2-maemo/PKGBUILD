# $Id$
# Maintainer: TheKit <nekit1000@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gtk2-maemo
pkgver=2.24.31
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (legacy with Maemo patches)"
arch=(i686 x86_64 arm armv7h)
url="http://www.gtk.org/"
conflicts=('gtk2')
provides=('gtk2')
depends=(atk pango libxcursor libxinerama libxrandr libxi libxcomposite libxdamage
         shared-mime-info cairo libcups gtk-update-icon-cache librsvg desktop-file-utils)
makedepends=(gobject-introspection python2 git gtk-doc)
optdepends=('gnome-themes-standard: Default widget theme'
            'adwaita-icon-theme: Default icon theme')
license=(LGPL)
install=gtk2.install
_commit=09c0b9c8a0f3dad599c179829ffb3a2e81f6efde
source=("git://git.gnome.org/gtk+#commit=$_commit"
        gtkrc
        gtk-query-immodules-2.0.hook
        xid-collision-debug.patch
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gdk-window.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-container.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-dialog.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-entry.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-enums.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-iconview.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-imcontext.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-menu.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-rbtree.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-textview.diff"
        "hildonize-gtk-treeview.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-widget.diff"
        "https://raw.githubusercontent.com/fremantle-gtk2/gtk/2.24.25-3m7%2Bmaemo/debian/patches/hildonize-gtk-window.diff")
sha256sums=('SKIP'
            'bc968e3e4f57e818430130338e5f85a5025e21d7e31a3293b8f5a0e58362b805'
            '9656a1efc798da1ac2dae94e921ed0f72719bd52d4d0138f305b993f778f7758'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            'c49d4c123c14c04144307ca89f4bb36628004e9327532c3ee95c46d63870bca5'
            '1e4af5f2d4003aac4f7bc01ed09d56824ac4349a1697963e895137dbde686963'
            '11861da101cff14de063ad03e6966a1538a3b867122adb1c669dba554a65fd72'
            '3ba40537602ca918ee5b79fb5558b96f4e47bc6738bce420882220a03c094e21'
            '7f2fe423d635c4ea1ae27767d7885400834db7e2495d367864f3ebcbe45fe935'
            '35e4d4399527e5a561ad98da239fdcfe6a4e5f2afae2d91504228f5567a9c8fa'
            '0ac5460974a36d8e26905dcd2a06e6fcb699b0ae1c55df66ee108de74a8923be'
            '4f065e50238cdcc1a49c9bbb0c182f2ff89f35ad17295e5bca50485d777f4b29'
            '49325fbb119d270f33051e26a58ccd0e1645f3ca322467cdfd19d19fbc7bc771'
            '787f54f4df2b8270366dbb3226608b776b36484cc1a1eba4889da48490cac5cc'
            '14c1af2243c8571006d6ccfb34bb974f3e5aa567936d313778b05a8ab68cca1e'
            'bd4b0e7e50e6fb15592cb5866fd04d1dc105f6b613e80808d5925971e61007db'
            '33f3b5ff75d3c9f9ab8a9575425cd4b806b186ef70b4158501f61fc0fa1ba80c')

pkgver() {
  cd gtk+
  git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd gtk+
    patch -Np1 -i ../xid-collision-debug.patch
    sed -i '1s/python$/&2/' gtk/gtk-builder-convert
    
    patch -Np1 -i ../hildonize-gtk-enums.diff
    patch -Np1 -i ../hildonize-gdk-window.diff
    patch -Np1 -i ../hildonize-gtk-container.diff
    patch -Np1 -i ../hildonize-gtk-window.diff
    patch -Np1 -i ../hildonize-gtk-menu.diff
    patch -Np1 -i ../hildonize-gtk-widget.diff
    patch -Np1 -i ../hildonize-gtk-imcontext.diff
    patch -Np1 -i ../hildonize-gtk-entry.diff
    patch -Np1 -i ../hildonize-gtk-dialog.diff
    patch -Np1 -i ../hildonize-gtk-textview.diff
    patch -Np1 -i ../hildonize-gtk-rbtree.diff
    patch -Np1 -i ../hildonize-gtk-treeview.diff
    patch -Np1 -i ../hildonize-gtk-iconview.diff    
    
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd gtk+

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes \
        --disable-gtk-doc

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd gtk+
    make DESTDIR="$pkgdir" install

    install -Dm644 ../gtkrc "$pkgdir/usr/share/gtk-2.0/gtkrc"
    install -Dm644 ../gtk-query-immodules-2.0.hook "$pkgdir/usr/share/libalpm/hooks/gtk-query-immodules-2.0.hook"

    rm "$pkgdir/usr/bin/gtk-update-icon-cache"
    #rm -r "$pkgdir/usr/share/gtk-doc"
}

# vim:set et sw=4:
